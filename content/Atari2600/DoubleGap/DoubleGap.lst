#### Labels
ADJUST_DIF       = 0x0348
AdjBump          = 0x037D
AdjCheckTable    = 0x0352
AdjNextRow       = 0x034A
BUILDROW         = 0x0090
Count100s        = 0x02A1
Count10s         = 0x02AC
CountDone        = 0x02B7
DIGITS           = 0x0544
DoGameOverMode   = 0x0042
DoPlayMode       = 0x003C
DrawFrame        = 0x0045
DrawVisibleRows  = 0x0060
EXPERTISE        = 0x031D
ExpDone          = 0x0345
ExpNoP1          = 0x0347
ExpP0Ama         = 0x032B
ExpP1            = 0x032D
ExpP1Ama         = 0x0343
F000             = 0x0000
F7FA             = 0x0594
FxRun            = 0x049D
GOMODE           = 0x0243
GR_PLAYER        = 0x053C
GapDone          = 0x029C
GapOK            = 0x028B
GoCheckP1        = 0x0225
GoKeepGoing      = 0x024D
GoP1Hit          = 0x0237
INIT             = 0x0107
INIT_GOMODE      = 0x0217
INIT_GO_FX       = 0x0471
INIT_MUSIC       = 0x039B
INIT_PLAYMODE    = 0x0150
INIT_SELMODE     = 0x01DB
MA1_01           = 0x04A4
MA1_END          = 0x04B8
MA2_01           = 0x04BC
MA2_END          = 0x04CD
MAKE_SCORE       = 0x029D
MA_SONG_1        = 0x04A0
MA_SONG_2        = 0x04B8
MB1_01           = 0x04D1
MB1_END          = 0x04E3
MB2_01           = 0x04E7
MB2_END          = 0x04F9
MB_SONG_1        = 0x04CD
MB_SONG_2        = 0x04E3
MOVE_WALLS       = 0x024E
MUSICA           = 0x04A0
MUSICB           = 0x04CD
MoveP0Left       = 0x01B6
MoveP0Right      = 0x01B1
MoveP1Left       = 0x01D6
MoveP1Right      = 0x01D1
MusChanA         = 0x03BC
MusChanB         = 0x0418
MusCmdCtrlA      = 0x03DA
MusCmdCtrlB      = 0x0436
MusCmdJumpA      = 0x03E7
MusCmdJumpB      = 0x0443
MusCmdToneA      = 0x03F5
MusCmdToneB      = 0x0451
MusDoB           = 0x0414
MusDoDone        = 0x0470
MusNoteA         = 0x03FF
MusNoteB         = 0x045B
NEW_GAPS         = 0x026F
NoFirst          = 0x0188
NoHit            = 0x019A
NoSelect         = 0x0171
NoWall           = 0x00CD
PLAYMODE         = 0x0164
PROCESS_GO_FX    = 0x0488
PROCESS_MUSIC    = 0x03B8
SELMODE          = 0x01EA
SEL_RESET_CHK    = 0x0386
SKILL_VALUES     = 0x04F9
SelDebounce      = 0x0398
SelExp           = 0x0213
SelP1On          = 0x0209
SelStartGame     = 0x0210
SetMoveP0        = 0x01B8
SetMoveP1        = 0x01D8
ShowP0           = 0x00A9
ShowP1           = 0x00AF
ShowScore        = 0x00D6
TimeP0Pos        = 0x011D
TimeP1Pos        = 0x0124
VIDEO_KERNEL     = 0x000B
WallDone         = 0x026C
WallOK           = 0x026A
main             = 0x0000

#### Defines
AUDC0            = 0x0015
AUDC1            = 0x0016
AUDF0            = 0x0017
AUDF1            = 0x0018
AUDV0            = 0x0019
AUDV1            = 0x001A
COLUBK           = 0x0009
COLUP0           = 0x0006
COLUP1           = 0x0007
COLUPF           = 0x0008
CTRLPF           = 0x000A
CXBLPF           = 0x0006
CXCLR            = 0x002C
CXM0FB           = 0x0004
CXM0P            = 0x0000
CXM1FB           = 0x0005
CXM1P            = 0x0001
CXP0FB           = 0x0002
CXP1FB           = 0x0003
CXPPMM           = 0x0007
DEBOUNCE         = 0x0090
ENABL            = 0x001F
ENAM0            = 0x001D
ENAM1            = 0x001E
ENTROPYA         = 0x008D
ENTROPYB         = 0x008E
ENTROPYC         = 0x008F
GAPBITS          = 0x0096
GRP0             = 0x001B
GRP1             = 0x001C
HMBL             = 0x0024
HMCLR            = 0x002B
HMM0             = 0x0022
HMM1             = 0x0023
HMOVE            = 0x002A
HMP0             = 0x0020
HMP1             = 0x0021
INPT0            = 0x0008
INPT1            = 0x0009
INPT2            = 0x000A
INPT3            = 0x000B
INPT4            = 0x000C
INPT5            = 0x000D
INTIM            = 0x0284
MODE             = 0x0088
MUSADEL          = 0x00A3
MUSAIND          = 0x00A4
MUSAVOL          = 0x00A5
MUSBDEL          = 0x00A6
MUSBIND          = 0x00A7
MUSBVOL          = 0x00A8
MUSCMD_CONTROL   = 0x0001
MUSCMD_JUMP      = 0x0000
MUSCMD_VOLUME    = 0x0002
MUS_DEL_1        = 0x0020
MUS_DEL_2        = 0x0040
MUS_DEL_3        = 0x0060
MUS_DEL_4        = 0x0080
MUS_REST         = 0x001F
MUS_TMP0         = 0x0085
MUS_TMP1         = 0x0086
NUSIZ0           = 0x0004
NUSIZ1           = 0x0005
PEFP1            = 0x000C
PF0              = 0x000D
PF1              = 0x000E
PF2              = 0x000F
PLAYR0Y          = 0x0083
PLAYR1Y          = 0x0084
REFP0            = 0x000B
RESBL            = 0x0014
RESM0            = 0x0012
RESM1            = 0x0013
RESMP0           = 0x0028
RESMP1           = 0x0029
RESP0            = 0x0010
RESP1            = 0x0011
RSYNC            = 0x0003
SCANCNT          = 0x0087
SCORE_PF1        = 0x0097
SCORE_PF2        = 0x009D
SWACNT           = 0x0281
SWBCNT           = 0x0283
SWCHA            = 0x0280
SWCHB            = 0x0282
TIM1024T         = 0x0297
TIM1T            = 0x0294
TIM64T           = 0x0296
TIM8T            = 0x0295
TMP0             = 0x0080
TMP1             = 0x0081
TMP2             = 0x0082
VBLANK           = 0x0001
VDELBL           = 0x0027
VDELP0           = 0x0025
VDELP1           = 0x0026
VSYNC            = 0x0000
WALLCNT          = 0x008A
WALLDELY         = 0x008B
WALLDELYR        = 0x008C
WALLDRELA        = 0x0091
WALLDRELB        = 0x0092
WALLDRELC        = 0x0093
WALLHEI          = 0x0095
WALLSTART        = 0x0094
WALL_INC         = 0x0089
WSYNC            = 0x0002
_CPU             = 6502

                  ._CPU = 6502
                  
                  ; Hardware definitions
                  ; On the web: [http://www.alienbill.com/2600/101/docs/stella.html Stella Programmer's Guide]
                  
                  ; The address decoding for the 2600 is very simple. The TIA is accessed
                  ; if A12 is 0 and A7 is 0. The TIA is selected if (address&1080 == 0000).
                  ;
                  ; The TIA chip uses the 6 lower address lines A0-A5. The reads seem to
                  ; ignore A4 and A5.
                  ;
                  ; The addresses have different read/write definitions and many of them
                  ; are strobes that are triggered by writing any value.
                  ;
                  ; Note that the 6502 has an 8-bit stack pointer. The CPU automatically sets the upper
                  ; byte of the address to 01. The RIOT chip RAM ghosts from 00xx to 01xx putting the stack
                  ; in RAM.
                  
                  ; TIA
                  
                  ; Reads
                  .CXM0P    = 0x0000 ; Collisions M0/P1
                  .CXM1P    = 0x0001 ; Collisions M1/P0
                  .CXP0FB   = 0x0002 ; Collisions M0/PF
                  .CXP1FB   = 0x0003 ; Collisions P1/PF
                  .CXM0FB   = 0x0004 ; Collisions M0/PF
                  .CXM1FB   = 0x0005 ; Collisions M1/PF
                  .CXBLPF   = 0x0006 ; Collisions BL/PF
                  .CXPPMM   = 0x0007 ; Collisions P0/P1
                  .INPT0    = 0x0008 ; Paddle input 0
                  .INPT1    = 0x0009 ; Paddle input 1
                  .INPT2    = 0x000A ; Paddle input 2
                  .INPT3    = 0x000B ; Paddle input 3
                  .INPT4    = 0x000C ; Latched inputs (joystick buttons)
                  .INPT5    = 0x000D ; Latched inputs (joystick buttons)
                  
                  ; Writes
                  .VSYNC    = 0x0000 ; D1=1 starts the vertical sync
                  .VBLANK   = 0x0001 ; D1=1 starts the vertical blank (D6 and D7 config INPT0-5)
                  .WSYNC    = 0x0002 ; Halts processor until leading edge of horizontal blank
                  .RSYNC    = 0x0003 ; Resets sync counter (used in chip testing)
                  .NUSIZ0   = 0x0004 ; Number and size of player/missile P0
                  .NUSIZ1   = 0x0005 ; Number and size of player/missile P0
                  .COLUP0   = 0x0006 ; Color-luminance of player 0
                  .COLUP1   = 0x0007 ; Color-luminance of player 1
                  .COLUPF   = 0x0008 ; Color-luminance of play filed
                  .COLUBK   = 0x0009 ; Color-luminance of background
                  .CTRLPF   = 0x000A ; Control playfield, priorities, and ball size
                  .REFP0    = 0x000B ; D3=1 reflects player 0
                  .PEFP1    = 0x000C ; D3=1 reflects player 1
                  .PF0      = 0x000D ; Playfield 0 bits (upper 4 bits)
                  .PF1      = 0x000E ; Playfield 1 bits
                  .PF2      = 0x000F ; Playfield 2 bits
                  .RESP0    = 0x0010 ; Set horizontal position of player 0
                  .RESP1    = 0x0011 ; Set horizontal position of player 1
                  .RESM0    = 0x0012 ; Set horizontal position of missile 0
                  .RESM1    = 0x0013 ; Set horizontal position of missile 1
                  .RESBL    = 0x0014 ; Set horizontal position of ball
                  .AUDC0    = 0x0015 ; Noise, tone, division control channel 0
                  .AUDC1    = 0x0016 ; Noise, tone, division control channel 1
                  .AUDF0    = 0x0017 ; Frequency divider channel 0
                  .AUDF1    = 0x0018 ; Frequency divider channel 1
                  .AUDV0    = 0x0019 ; Volume channel 0
                  .AUDV1    = 0x001A ; Volume channel 1
                  .GRP0     = 0x001B ; Player 0 graphics
                  .GRP1     = 0x001C ; Player 1 graphics
                  .ENAM0    = 0x001D ; D1=1 to enable missile 0
                  .ENAM1    = 0x001E ; D1=1 to enable missile 1
                  .ENABL    = 0x001F ; D1=1 to enable ball
                  .HMP0     = 0x0020 ; Horizontal motion offset for player 0
                  .HMP1     = 0x0021 ; Horizontal motion offset for player 1
                  .HMM0     = 0x0022 ; Horizontal motion offset for missile 0
                  .HMM1     = 0x0023 ; Horizontal motion offset for missile 1
                  .HMBL     = 0x0024 ; Horizontal motion offset for ball
                  .VDELP0   = 0x0025 ; D0=1 to delay player 0 one vertical line
                  .VDELP1   = 0x0026 ; D0=1 to delay player 1 one vertical line
                  .VDELBL   = 0x0027 ; D0=1 to delay ball one vertical line
                  .RESMP0   = 0x0028 ; D1=1 to reset missile 0 to player 0
                  .RESMP1   = 0x0029 ; D1=1 to reset missile 1 to player 1
                  .HMOVE    = 0x002A ;  Executes horizontal motions
                  .HMCLR    = 0x002B ; Clears all horizonal motion registers
                  .CXCLR    = 0x002C ; Clears all collision registers
                  
                  ; PIA
                  
                  ; The PIA (R.I.O.T.) is accessed if A12 is 0 and A7 is 1. If A9 is 0 then the 128 bytes
                  ; of RAM are selected. If A9 is 1 then the PIA timer registers are selected.
                  ;
                  ; The TIA REGS are selected if (address&1280 == 0280).
                  ;
                  ; The TIA RAM is selected if (address&1280 == 0080).
                  ;
                  ; The PIA chip uses the lower 7 address lines A0-A6. Thus the 007F mask below.
                  
                  .SWCHA    = 0x0280 ; Port A Hand controllers
                  .SWACNT   = 0x0281 ; Port A DDR
                  
                  .SWCHB    = 0x0282 ; Port B Console switches
                  ; D7 : P1 difficulty (0=amateur, 1=pro)
                  ; D6 : P0 difficulty (0=amateur, 1=pro)
                  ; D5 : not used
                  ; D4 : not used
                  ; D3 : color or B/W (0=B/W, 1=color)
                  ; D2 : not used
                  ; D1 : game select (0=pressed)
                  ; D0 : game reset (0=pressed)
                  
                  .SWBCNT   = 0x0283 ; Port B DDR
                  .INTIM    = 0x0284 ; Timer output (read only)
                  .TIM1T    = 0x0294 ; Set 1 clock interval (write only)
                  .TIM8T    = 0x0295 ; Set 8 clock interval (write only)
                  .TIM64T   = 0x0296 ; Set 64 clock interval (write only)
                  .TIM1024T = 0x0297 ; Set 1024 clock interval (write only)
                  
                  ; RAM Usage
                  .TMP0      = 128
                  .TMP1      = 129
                  .TMP2      = 130
                  .PLAYR0Y   = 131
                  .PLAYR1Y   = 132
                  .MUS_TMP0  = 133
                  .MUS_TMP1  = 134
                  .SCANCNT   = 135
                  .MODE      = 136
                  .WALL_INC  = 137
                  .WALLCNT   = 138
                  .WALLDELY  = 139
                  .WALLDELYR = 140
                  .ENTROPYA  = 141
                  .ENTROPYB  = 142
                  .ENTROPYC  = 143
                  .DEBOUNCE  = 144
                  .WALLDRELA = 145
                  .WALLDRELB = 146
                  .WALLDRELC = 147
                  .WALLSTART = 148
                  .WALLHEI   = 149
                  .GAPBITS   = 150
                  .SCORE_PF1 = 151
                  .SCORE_PF2 = 157
                  .MUSADEL   = 163
                  .MUSAIND   = 164
                  .MUSAVOL   = 165
                  .MUSBDEL   = 166
                  .MUSBIND   = 167
                  .MUSBVOL   = 168
                  
                  F000:
                  main:
0000: 78               SEI                       ; Turn off interrupts
0001: D8               CLD                       ; Clear the "decimal" flag
0002: A2 FF            LDX      #0xFF            ; Set stack ...
0004: 9A               TXS                       ; ... to the end of RAM
0005: 20 07 01         JSR      INIT             ; Initialize game environment
0008: 20 DB 01         JSR      INIT_SELMODE     ; Start out in SELECT-MODE
                  
                  VIDEO_KERNEL:
                  
000B: A9 02            LDA      #2               ; D1 bit ON
000D: 85 02            STA      WSYNC            ; Wait for the end of the current line
000F: 85 01            STA      VBLANK           ; Turn the electron beam off
0011: 85 02            STA      WSYNC            ; Wait for all ...
0013: 85 02            STA      WSYNC            ; ... the electrons ...
0015: 85 02            STA      WSYNC            ; ... to drain out.
0017: 85 00            STA      VSYNC            ; Trigger the vertical sync signal
0019: 85 02            STA      WSYNC            ; Hold the vsync signal for ...
001B: 85 02            STA      WSYNC            ; ... three ...
001D: 85 02            STA      WSYNC            ; ... scanlines
001F: 85 2A            STA      HMOVE            ; Tell hardware to move all game objects
0021: A9 00            LDA      #0               ; D1 bit OFF
0023: 85 00            STA      VSYNC            ; Release the vertical sync signal
0025: A9 2B            LDA      #43              ; Set timer to 43*64 = 2752 machine ...
0027: 8D 96 02         STA      TIM64T           ; ... cycles 2752/(228/3) = 36 scanlines
                  
                  ;  ***** LENGTHY GAME LOGIC PROCESSING BEGINS HERE *****
                  
                  ;  Do one of 3 routines while the beam travels back to the top
                  ;  0 = Game Over processing
                  ;  1 = Playing-Game processing
                  ;  2 = Selecting-Game processing
                  
002A: E6 8D            INC      ENTROPYA         ; Counting video frames as part of the random number
002C: A5 88            LDA      MODE             ; What are we doing between frames?
                  
002E: C9 00            CMP      #0               ; Mode is ...
0030: F0 10            BEQ      DoGameOverMode   ; ... "game over"
0032: C9 01            CMP      #1               ; Mode is ...
0034: F0 06            BEQ      DoPlayMode       ; ... "game play"
0036: 20 EA 01         JSR      SELMODE          ; Mode is "select game"
0039: 4C 45 00         JMP      DrawFrame        ; Continue to the visible screen area
                  
                  DoPlayMode:
003C: 20 64 01         JSR      PLAYMODE         ; Playing-game processing
003F: 4C 45 00         JMP      DrawFrame        ; Continue to the visible screen area
                  
                  DoGameOverMode:
0042: 20 43 02         JSR      GOMODE           ; Game-over processing
                  
                  ;  ***** LENGTHY GAME LOGIC PROCESSING ENDS HERE *****
                  
                  DrawFrame:
0045: AD 84 02         LDA      INTIM            ; Wait for ...
0048: C9 00            CMP      #0               ; ... the visible area ...
004A: D0 F9            BNE      DrawFrame        ; ... of the screen
                  
004C: 85 02            STA      WSYNC            ; 37th scanline
004E: A9 00            LDA      #0               ; Turn the ...
0050: 85 01            STA      VBLANK           ; ... electron beam back on
                  
0052: A9 00            LDA      #0               ; Zero out ...
0054: 85 87            STA      SCANCNT          ; ... scanline count ...
0056: 85 80            STA      TMP0             ; ... and all ...
0058: 85 81            STA      TMP1             ; ... returns ...
005A: 85 82            STA      TMP2             ; ... expected ...
005C: AA               TAX                       ; ... to come from ...
005D: A8               TAY                       ; ... BUILDROW
                  
005E: 85 2C            STA      CXCLR            ; Clear collision detection
                  
                  DrawVisibleRows:
                  
0060: A5 80            LDA      TMP0             ; Get A ready (PF0 value)
0062: 85 02            STA      WSYNC            ; Wait for very start of row
0064: 86 1B            STX      GRP0             ; Player 0 -- in X
0066: 84 1C            STY      GRP1             ; Player 1 -- in Y
0068: 85 0D            STA      PF0              ; PF0      -- in TMP0 (already in A)
006A: A5 81            LDA      TMP1             ; PF1      -- in TMP1
006C: 85 0E            STA      PF1              ; ...
006E: A5 82            LDA      TMP2             ; PP2      -- in TMP2
0070: 85 0F            STA      PF2              ; ...
                  
0072: 20 90 00         JSR      BUILDROW         ; This MUST take through to the next line
                  
0075: E6 87            INC      SCANCNT          ; Next scan line
0077: A5 87            LDA      SCANCNT          ; Do 109*2 = 218 lines
0079: C9 6D            CMP      #109             ; All done?
007B: D0 E3            BNE      DrawVisibleRows  ; No ... get all the visible rows
                  
                  ;  END VISIBLE PART OF FRAME
                  
007D: A9 00            LDA      #0               ; Turn off electron beam
007F: 85 02            STA      WSYNC            ; Next scanline
0081: 85 0D            STA      PF0              ; Play field 0 off
0083: 85 1B            STA      GRP0             ; Player 0 off
0085: 85 1C            STA      GRP1             ; Player 1 off
0087: 85 0E            STA      PF1              ; Play field 1 off
0089: 85 0F            STA      PF2              ; Play field 2 off
008B: 85 02            STA      WSYNC            ; Next scanline
                  
008D: 4C 0B 00         JMP      VIDEO_KERNEL
                  
                  BUILDROW:
                  
0090: A5 87            LDA      SCANCNT          ; Where are we on the screen?
                  
0092: C9 06            CMP      #6               ; If we are in the ...
0094: 90 40            BCC      ShowScore        ; ... score area
                  
0096: 29 07            AND      #7               ; Lower 3 bits as an index again
0098: A8               TAY                       ; Using Y to lookup graphics
0099: B9 3C 05         LDA      GR_PLAYER,Y      ; Get the graphics (if enabled on this row)
009C: AA               TAX                       ; Hold it (for return as player 0)
009D: A8               TAY                       ; Hold it (for return as player 1)
009E: A5 87            LDA      SCANCNT          ; Scanline count again
00A0: 4A               LSR      A                ; This time ...
00A1: 4A               LSR      A                ; ... we divide ...
00A2: 4A               LSR      A                ; ... by eight (8 rows in picture)
                  
00A3: C5 83            CMP      PLAYR0Y          ; Scanline group of the P0 object?
00A5: F0 02            BEQ      ShowP0           ; Yes ... keep the picture
00A7: A2 00            LDX      #0               ; Not time for Player 0 ... no graphics
                  ShowP0:
                  
00A9: C5 84            CMP      PLAYR1Y          ; Scanline group of the P1 object?
00AB: F0 02            BEQ      ShowP1           ; Yes ... keep the picture
00AD: A0 00            LDY      #0               ; Not time for Player 0 ... no graphics
                  ShowP1:
                  
00AF: A5 94            LDA      WALLSTART        ; Calculate ...
00B1: 18               CLC                       ; ... the bottom ...
00B2: 65 95            ADC      WALLHEI          ; ... of ...
00B4: 85 80            STA      TMP0             ; ... the wall
                  
00B6: A5 87            LDA      SCANCNT          ; Scanline count
                  
00B8: C5 94            CMP      WALLSTART        ; Past upper part of wall?
00BA: 90 11            BCC      NoWall           ; No ... skip it
00BC: C5 80            CMP      TMP0             ; Past lower part of wall
00BE: B0 0D            BCS      NoWall           ; Yes ... skip it
                  
                  ;  The wall is on this row
00C0: A5 91            LDA      WALLDRELA        ; Draw wall ...
00C2: 85 80            STA      TMP0             ; ... by transfering ...
00C4: A5 92            LDA      WALLDRELB        ; ... playfield ...
00C6: 85 81            STA      TMP1             ; ... patterns ...
00C8: A5 93            LDA      WALLDRELC        ; ... to ...
00CA: 85 82            STA      TMP2             ; ... return area
00CC: 60               RTS                       ; Done
                  
                  NoWall:
                  ;  The wall is NOT on this row
00CD: A9 00            LDA      #0               ; No walls on this row
00CF: 85 80            STA      TMP0             ; ... clear ...
00D1: 85 81            STA      TMP1             ; ... out ...
00D3: 85 82            STA      TMP2             ; ... the playfield
00D5: 60               RTS                       ; Done
                  
                  ShowScore:
00D6: 29 07            AND      #7               ; OLine=182  Only need the lower 3 bits
00D8: A8               TAY                       ; OLine=183  Soon to be an index into a list
                  
                  ;  At this point, the beam is past the loading of the
                  ;  playfield for the left half. We want to make sure
                  ;  that the right half of the playfield is off, so do that
                  ;  now.
                  
00D9: A2 00            LDX      #0               ; Blank bit pattern
00DB: 86 80            STX      TMP0             ; This will always be blank
00DD: 86 0E            STX      PF1              ; Turn off playfield ...
00DF: 86 0F            STX      PF2              ; ... for right half of the screen
                  
00E1: AA               TAX                       ; Another index
00E2: B9 97 00         LDA      SCORE_PF1,Y      ; Lookup the PF1 graphics for this row
00E5: 85 81            STA      TMP1             ; Return it to the caller
00E7: A8               TAY                       ; We'll need this value again in a second
00E8: B5 9D            LDA      SCORE_PF2,X      ; Lookup the PF2 graphics for this row
00EA: 85 82            STA      TMP2             ; Return it to the caller
                  
00EC: 85 02            STA      WSYNC            ; Now on the next row
                  
00EE: 84 0E            STY      PF1              ; Repeat the left-side playfield ...
00F0: 85 0F            STA      PF2              ; ... onto the new row
                  
00F2: B5 9D            LDA      SCORE_PF2,X      ; Kill some time waiting for the ...
00F4: B5 9D            LDA      SCORE_PF2,X      ; ... beam to pass the left half ...
00F6: B5 9D            LDA      SCORE_PF2,X      ; ... of the playfield again
00F8: B5 9D            LDA      SCORE_PF2,X      ; ...
00FA: B5 9D            LDA      SCORE_PF2,X      ; ...
00FC: B5 9D            LDA      SCORE_PF2,X      ; ...
                  
00FE: A2 00            LDX      #0               ; Return 0 (off) for player 0 ...
0100: A0 00            LDY      #0               ; ... and player 1
                  
                  ;  The beam is past the left half of the field again.
                  ;  Turn off the playfield.
                  
0102: 86 0E            STX      PF1              ; 0 to PF1 ...
0104: 86 0F            STX      PF2              ; ... and PF2
0106: 60               RTS                       ;  Done
                  
                  INIT:
                  ;  This function is called ONCE at power-up/reset to initialize various
                  ;  game settings and variables.
                  
0107: A9 40            LDA      #64              ; Wall is ...
0109: 85 08            STA      COLUPF           ; ... redish
010B: A9 7E            LDA      #126             ; P0 is ...
010D: 85 06            STA      COLUP0           ; ... white
010F: A9 00            LDA      #0               ; P1 ...
0111: 85 07            STA      COLUP1           ; ... black
                  
0113: A9 05            LDA      #5               ; Right half of playfield is reflection of left ...
0115: 85 0A            STA      CTRLPF           ; ... and playfield is on top of players
                  
                  ; TODO other hardware inits here
                  
0117: A2 04            LDX      #4               ; Player 0 position count
0119: A0 03            LDY      #3               ; Player 1 position count
011B: 85 02            STA      WSYNC            ; Get a fresh scanline
                  
                  TimeP0Pos:
011D: CA               DEX                       ; Kill time while the beam moves ...
011E: E0 00            CPX      #0               ; ... to desired ...
0120: D0 FB            BNE      TimeP0Pos        ; ... position
0122: 85 10            STA      RESP0            ; Mark player 0's X position
                  
                  TimeP1Pos:
0124: 88               DEY                       ; Kill time while the beam moves ...
0125: C0 00            CPY      #0               ; ... to desired ...
0127: D0 FB            BNE      TimeP1Pos        ; ... position
0129: 85 11            STA      RESP1            ; Mark player 1's X position
                  
012B: 20 1D 03         JSR      EXPERTISE        ; Initialize the players' Y positions base on expert-settings
                  
012E: A9 0A            LDA      #10              ; Wall is ...
0130: 85 95            STA      WALLHEI          ; ... 10 double-scanlines high
                  
0132: A9 00            LDA      #0               ; Set score to ...
0134: 85 8A            STA      WALLCNT          ; ... 0
0136: 20 9D 02         JSR      MAKE_SCORE       ; Blank the score digits
0139: A9 00            LDA      #0               ; Blank bits ...
013B: 85 A2            STA      SCORE_PF2+5      ; ... on the end of each ...
013D: 85 9C            STA      SCORE_PF1+5      ; ... digit pattern
                  
013F: 20 48 03         JSR      ADJUST_DIF       ; Initialize the wall parameters
0142: 20 6F 02         JSR      NEW_GAPS         ; Build the wall's initial gap
                  
0145: A9 70            LDA      #112             ; Set wall position off bottom ...
0147: 85 94            STA      WALLSTART        ; ... to force a restart on first move
                  
0149: A9 00            LDA      #0               ; Zero out ...
014B: 85 20            STA      HMP0             ; ... player 0 motion ...
014D: 85 21            STA      HMP1             ; ... and player 1 motion
                  
014F: 60               RTS                       ; Done
                  
                  INIT_PLAYMODE:
                  
                  ;  This function initializes the game play mode
                  
0150: A9 C0            LDA      #192             ; Background is ...
0152: 85 09            STA      COLUBK           ; ... greenish
0154: A9 01            LDA      #1               ; Game mode is ...
0156: 85 88            STA      MODE             ; ... SELECT
0158: A9 FF            LDA      #255             ; Restart wall score to ...
015A: 85 8A            STA      WALLCNT          ; ... 0 on first move
015C: A9 70            LDA      #112             ; Force wall to start ...
015E: 85 94            STA      WALLSTART        ; ... over on first move
0160: 20 9B 03         JSR      INIT_MUSIC       ; Initialize the music
0163: 60               RTS                       ; Done
                  
                  PLAYMODE:
                  
                  ;  This function is called once per frame to process the main game play.
                  
                  
0164: 20 86 03         JSR      SEL_RESET_CHK    ; Check to see if Reset/Select has changed
                  
0167: C9 00            CMP      #0               ; Is select pressed?
0169: F0 06            BEQ      NoSelect         ; No ... skip
016B: 86 90            STX      DEBOUNCE         ; Restore the old value ...
016D: 20 DB 01         JSR      INIT_SELMODE     ; ... and let select-mode process the toggle
0170: 60               RTS                       ; Done
                  
                  NoSelect:
0171: 20 B8 03         JSR      PROCESS_MUSIC    ; Process any playing music
0174: 20 4E 02         JSR      MOVE_WALLS       ; Move the walls
                  
0177: C9 01            CMP      #1               ; Wall on first row?
0179: D0 0D            BNE      NoFirst          ; No ... move on
017B: E6 8A            INC      WALLCNT          ; Bump the score
017D: 20 48 03         JSR      ADJUST_DIF       ; Change the wall parameters based on score
0180: A5 8A            LDA      WALLCNT          ; Change the ...
0182: 20 9D 02         JSR      MAKE_SCORE       ; ... score pattern
0185: 20 6F 02         JSR      NEW_GAPS         ; Calculate the new gap position
                  
                  NoFirst:
0188: A5 02            LDA      CXP0FB          ; Player 0 collision with playfield
018A: 85 80            STA      TMP0            ; Hold it
018C: A5 03            LDA      CXP1FB          ; Player 1 collision with playfield
018E: 05 80            ORA      TMP0            ; Did either ...
0190: 29 80            AND      #128            ; ... player hit ...
0192: C9 00            CMP      #0              ; ... wall?
0194: F0 04            BEQ      NoHit           ; No ... move on
0196: 20 17 02         JSR      INIT_GOMODE     ; Go to Game-Over mode
0199: 60               RTS                      ; Done
                  
                  NoHit:
019A: AD 80 02         LDA      SWCHA           ; Joystick
019D: 29 80            AND      #128            ; Player 0 ...
019F: C9 00            CMP      #0              ; ... moving left?
01A1: F0 13            BEQ      MoveP0Left      ; Yes ... move left
01A3: AD 80 02         LDA      SWCHA           ; Joystick
01A6: 29 40            AND      #64             ; Player 0 ...
01A8: C9 00            CMP      #0              ; ... moving right?
01AA: F0 05            BEQ      MoveP0Right     ; Yes ... move right
01AC: A9 00            LDA      #0              ; Not moving value
01AE: 4C B8 01         JMP      SetMoveP0       ; Don't move the player
                  MoveP0Right:
01B1: A9 10            LDA      #16             ; +1
01B3: 4C B8 01         JMP      SetMoveP0       ; Set HMP0
                  MoveP0Left:
01B6: A9 F0            LDA      #240            ; -1
                  SetMoveP0:
01B8: 85 20            STA      HMP0            ; New movement value P0
                  
01BA: AD 80 02         LDA      SWCHA           ; Joystick
01BD: 29 08            AND      #8              ; Player 1 ...
01BF: C9 00            CMP      #0              ; ... moving left?
01C1: F0 13            BEQ      MoveP1Left      ; Yes ... move left
01C3: AD 80 02         LDA      SWCHA           ; Joystick
01C6: 29 04            AND      #4              ; Player 0 ...
01C8: C9 00            CMP      #0              ; ... moving right?
01CA: F0 05            BEQ      MoveP1Right     ; Yes ... move right
01CC: A9 00            LDA      #0              ; Not moving value
01CE: 4C D8 01         JMP      SetMoveP1       ; Don't move the player
                  MoveP1Right:
01D1: A9 10            LDA      #16             ; +1
01D3: 4C D8 01         JMP      SetMoveP1       ; Set HMP0
                  MoveP1Left:
01D6: A9 F0            LDA      #240            ; -1
                  SetMoveP1:
01D8: 85 21            STA      HMP1            ; New movement value P1
                  
01DA: 60               RTS                      ; Done
                  
                  INIT_SELMODE:
                  
                  ;  This function initializes the games SELECT-MODE
                  
01DB: A9 00            LDA      #0              ; Turn off ...
01DD: 85 19            STA      AUDV0           ; ... all ...
01DF: 85 1A            STA      AUDV1           ; ... sound
01E1: A9 C8            LDA      #200            ; Background ...
01E3: 85 09            STA      COLUBK          ; ... greenish bright
01E5: A9 02            LDA      #2              ; Now in ...
01E7: 85 88            STA      MODE            ; SELECT game mode
01E9: 60               RTS                      ; Done
                  
                  
                  SELMODE:
                  
                  ;  This function is called once per frame to process the SELECT-MODE.
                  ;  The wall moves here, but doesn't change or collide with players.
                  ;  This function selects between 1 and 2 player game.
                  
01EA: 20 4E 02         JSR      MOVE_WALLS       ; Move the walls
01ED: 20 86 03         JSR      SEL_RESET_CHK    ; Check the reset/select switches
01F0: C9 01            CMP      #1               ; RESET button?
01F2: F0 1C            BEQ      SelStartGame     ; Yes ... start game
01F4: C9 03            CMP      #3               ; RESET and SELECT?
01F6: F0 18            BEQ      SelStartGame     ; Yes ... start game
01F8: C9 02            CMP      #2               ; Select only?
01FA: D0 17            BNE      SelExp           ; No ... stay in this mode
01FC: A5 84            LDA      PLAYR1Y          ; Select toggled. Get player 1 Y coordinate
01FE: C9 FF            CMP      #255             ; 2nd player on the screen?
0200: F0 07            BEQ      SelP1On          ; No ... toggle it on
0202: A9 FF            LDA      #255             ; Yes ...
0204: 85 84            STA      PLAYR1Y          ; ... toggle it off
0206: 4C 13 02         JMP      SelExp           ; Move to expertise
                  SelP1On:
0209: A9 0C            LDA      #12              ; Y coordinate
020B: 85 84            STA      PLAYR1Y          ; On screen now
020D: 4C 13 02         JMP      SelExp           ; Move to expertise
                  
                  SelStartGame:
0210: 20 50 01         JSR      INIT_PLAYMODE    ; Reset toggled ... start game
                  SelExp:
0213: 20 1D 03         JSR      EXPERTISE        ; Adjust both players for pro settings
0216: 60               RTS                       ; Done
                  
                  INIT_GOMODE:
                  
                  ;  This function initializes the GAME-OVER game mode.
                  
0217: 85 2B            STA      HMCLR            ; Stop both players from moving
0219: A5 02            LDA      CXP0FB           ; P0 collision ...
021B: 29 80            AND      #128             ; ... with wall
021D: C9 00            CMP      #0               ; Did P0 hit the wall?
021F: D0 04            BNE      GoCheckP1        ; Yes ... leave it at bottom
0221: A9 02            LDA      #2               ; No ... move player 0 ...
0223: 85 83            STA      PLAYR0Y          ; ... up the screen to show win
                  
                  GoCheckP1:
0225: A5 03            LDA      CXP1FB           ; P1 collision ...
0227: 29 80            AND      #128             ; ... with wall
0229: C9 00            CMP      #0               ; Did P1 hit the wall?
022B: D0 0A            BNE      GoP1Hit          ; Yes ... leave it at the bottom
022D: A5 84            LDA      PLAYR1Y          ; Is P1 even ...
022F: C9 FF            CMP      #255             ; ... on the screen (2 player game?)
0231: F0 04            BEQ      GoP1Hit          ; No ... skip it
0233: A9 02            LDA      #2               ; Player 1 is onscreen and didn't collide ...
0235: 85 84            STA      PLAYR1Y          ; ... move up the screen to show win
                  
                  GoP1Hit:
0237: A9 00            LDA      #0               ; Going to ...
0239: 85 88            STA      MODE             ; ... game-over mode
023B: 85 19            STA      AUDV0            ; Turn off any ...
023D: 85 1A            STA      AUDV1            ; ... sound
023F: 20 71 04         JSR      INIT_GO_FX       ; Initialize sound effects
0242: 60               RTS                       ; Done
                  
                  GOMODE:
                  
                  ; This function is called every frame to process the game
                  ; over sequence. When the sound effect has finished, the
                  ; game switches to select mode.
                  
0243: 20 88 04         JSR      PROCESS_GO_FX    ; Process the sound effects
0246: C9 00            CMP      #0               ; Effects still running?
0248: F0 03            BEQ      GoKeepGoing      ; Yes ... let them run
024A: 20 DB 01         JSR      INIT_SELMODE     ; When effect is over, go to select mode
                  GoKeepGoing:
024D: 60               RTS                       ; Done
                  
                  MOVE_WALLS:
                  
                  ;  This function moves the wall down the screen and back to position 0
                  ;  when it reaches (or passes) 112.
                  
024E: C6 8B            DEC      WALLDELY         ; Wall motion timer
0250: A5 8B            LDA      WALLDELY         ; Time to ...
0252: C9 00            CMP      #0               ; ... move the wall?
0254: D0 16            BNE      WallDone         ; No ... leave it alone
0256: A5 8C            LDA      WALLDELYR        ; Reset the ...
0258: 85 8B            STA      WALLDELY         ; ... delay count
025A: A5 94            LDA      WALLSTART        ; Current wall position
025C: 18               CLC                       ; Increment ...
025D: 65 89            ADC      WALL_INC         ; ... wall position
025F: C9 70            CMP      #112             ; At the bottom?
0261: 90 07            BCC      WallOK           ; No ... leave it alone
0263: A9 00            LDA      #0               ; Else restart ...
0265: 85 94            STA      WALLSTART        ; ... wall at top of screen
0267: A9 01            LDA      #1               ; Return flag that wall DID restart
0269: 60               RTS                       ; Done
                  WallOK:
026A: 85 94            STA      WALLSTART        ; Store new wall position
                  WallDone:
026C: A9 00            LDA      #0               ; Return flag that wall did NOT restart
026E: 60               RTS                       ; Done
                  
                  
                  NEW_GAPS:
                  
                  ;  This function builds the PF0, PF1, and PF2 graphics for a wall
                  ;  with the gap pattern (GAPBITS) placed at random in the 20 bit
                  ;  area.
                  
026F: A9 FF            LDA      #255             ; Start with ...
0271: 85 91            STA      WALLDRELA        ; ... solid wall in PF0 ...
0273: 85 92            STA      WALLDRELB        ; ... and PF1
0275: A5 96            LDA      GAPBITS          ; Store the gap pattern ...
0277: 85 93            STA      WALLDRELC        ; ... in PF2
                  
0279: A5 8D            LDA      ENTROPYA         ; Get ...
027B: 65 8E            ADC      ENTROPYB         ; ... a randomish ...
027D: 65 8F            ADC      ENTROPYC         ; ... number ...
027F: 85 8F            STA      ENTROPYC         ; Update the random seed
0281: 29 0F            AND      #15              ; 0 to 15
0283: C9 0C            CMP      #12              ; Too far to the right?
0285: F0 04            BEQ      GapOK            ; No ... 12 is OK
0287: 90 02            BCC      GapOK            ; No ... less than 12 is OK
0289: E9 09            SBC      #9               ; Back up 9
                  
                  GapOK:
028B: C9 00            CMP      #0               ; Gap already at far left?
028D: F0 0D            BEQ      GapDone          ; Yes ... done
028F: 38               SEC                       ; Roll gap ...
0290: 66 93            ROR      WALLDRELC        ; ... left ...
0292: 26 92            ROL      WALLDRELB        ; ... desired ...
0294: 66 91            ROR      WALLDRELA        ; ... times ...
0296: 38               SEC                       ; All rolls ...
0297: E9 01            SBC      #1               ; ... done?
0299: 4C 8B 02         JMP      GapOK            ; No ... do them all
                  GapDone:
029C: 60               RTS                       ; New wall pattern is ready
                  
                  MAKE_SCORE:
                  
                  ;  This function builds the PF1 and PF2 graphics rows for
                  ;  the byte value passed in A. The current implementation is
                  ;  two-digits only ... PF2 is blank.
                  
029D: A2 00            LDX      #0               ; 100's digit
029F: A0 00            LDY      #0               ; 10's digit
                  
                  Count100s:
02A1: C9 64            CMP      #100             ; Need another 100s digit?
02A3: 90 07            BCC      Count10s         ; No ... move on to 10s
02A5: E8               INX                       ; Count ...
02A6: 38               SEC                       ; ... value
02A7: E9 64            SBC      #100             ; Take off this 100
02A9: 4C A1 02         JMP      Count100s        ; Keep counting
                  Count10s:
02AC: C9 0A            CMP      #10              ; Need another 10s digit?
02AE: 90 07            BCC      CountDone        ; No ... got all the tens
02B0: C8               INY                       ; Count ...
02B1: 38               SEC                       ; ... value
02B2: E9 0A            SBC      #10              ; Take off this 10
02B4: 4C AC 02         JMP      Count10s         ; Keep counting
                  
                  CountDone:
02B7: 0A               ASL      A                ; One's digit ...
02B8: 0A               ASL      A                ; ... *8 ....
02B9: 0A               ASL      A                ; ... to find picture
02BA: AA               TAX                       ; One's digit picture to X
02BB: 98               TYA                       ; Now the 10's digit
02BC: 0A               ASL      A                ; Multiply ...
02BD: 0A               ASL      A                ; ... by 8 ...
02BE: 0A               ASL      A                ; ... to find picture
02BF: A8               TAY                       ; 10's picture in Y
                  
02C0: B9 44 05         LDA      DIGITS,Y         ; Get the 10's digit
02C3: 29 F0            AND      #0xF0            ; Upper nibble
02C5: 85 97            STA      SCORE_PF1        ; Store left side
02C7: BD 44 05         LDA      DIGITS,X         ; Get the 1's digit
02CA: 29 0F            AND      #0x0F            ; Lower nibble
02CC: 05 97            ORA      SCORE_PF1        ; Put left and right half together
02CE: 85 97            STA      SCORE_PF1        ; And store image
                  
                  ; We have plenty of code space. Time and registers are at a premium.
                  ; So copy/past the code for each row
                  
02D0: B9 45 05         LDA      DIGITS+1,Y       ; Repeat for 2nd line of picture ...
02D3: 29 F0            AND      #0xF0            ; ...
02D5: 85 98            STA      SCORE_PF1+1      ; ...
02D7: BD 45 05         LDA      DIGITS+1,X       ; ...
02DA: 29 0F            AND      #15              ; ...
02DC: 05 98            ORA      SCORE_PF1+1      ; ...
02DE: 85 98            STA      SCORE_PF1+1      ; ...
                  
02E0: B9 46 05         LDA      DIGITS+2,Y       ; Repeat for 3nd line of picture
02E3: 29 F0            AND      #0xF0            ; ...
02E5: 85 99            STA      SCORE_PF1+2      ; ...
02E7: BD 46 05         LDA      DIGITS+2,X       ; ...
02EA: 29 0F            AND      #0x0F            ; ...
02EC: 05 99            ORA      SCORE_PF1+2      ; ...
02EE: 85 99            STA      SCORE_PF1+2      ; ...
                  
02F0: B9 47 05         LDA      DIGITS+3,Y       ; Repeat for 4th line of picture
02F3: 29 F0            AND      #0xF0            ; ...
02F5: 85 9A            STA      SCORE_PF1+3      ; ...
02F7: BD 47 05         LDA      DIGITS+3,X       ; ...
02FA: 29 0F            AND      #0x0F            ; ...
02FC: 05 9A            ORA      SCORE_PF1+3      ; ...
02FE: 85 9A            STA      SCORE_PF1+3      ; ...
                  
0300: B9 48 05         LDA      DIGITS+4,Y       ; Repeat for 5th line of picture
0303: 29 F0            AND      #0xF0            ; ...
0305: 85 9B            STA      SCORE_PF1+4      ; ...
0307: BD 48 05         LDA      DIGITS+4,X       ; ...
030A: 29 0F            AND      #0x0F            ; ...
030C: 05 9B            ORA      SCORE_PF1+4      ; ...
030E: 85 9B            STA      SCORE_PF1+4      ; ...
                  
0310: A9 00            LDA      #0               ; For now ...
0312: 85 9D            STA      SCORE_PF2        ; ... there ...
0314: 85 9E            STA      SCORE_PF2+1      ; ... is ...
0316: 85 9F            STA      SCORE_PF2+2      ; ... no ...
0318: 85 A0            STA      SCORE_PF2+3      ; ... 100s ...
031A: 85 A1            STA      SCORE_PF2+4      ; ... digit drawn
                  
031C: 60               RTS                       ; Done
                  
                  EXPERTISE:
                  
                  ;  This function changes the Y position of the players based on the
                  ;  position of their respective pro/novice switches. The player 1
                  ;  position is NOT changed if the mode is a single-player game.
                  
031D: AD 82 02         LDA      SWCHB            ; Check P0 ...
0320: 29 40            AND      #0x40            ; ... pro/novice settings
0322: C9 00            CMP      #0               ; Amateur?
0324: F0 05            BEQ      ExpP0Ama         ; Yes ... near the bottom of screen
0326: A9 08            LDA      #8               ; Pro ... near the top
0328: 4C 2D 03         JMP      ExpP1            ; Store and check P0
                  ExpP0Ama:
032B: A9 0C            LDA      #12              ; near the bottom
                  
                  ExpP1:
032D: 85 83            STA      PLAYR0Y          ; Player 0 Y coordinate
                  
032F: A6 84            LDX      PLAYR1Y          ; Is P1 on ...
0331: E0 FF            CPX      #255             ; ... the screen?
0333: F0 12            BEQ      ExpNoP1          ; No ... skip all this
0335: AD 82 02         LDA      SWCHB            ; Check P1 ...
0338: 29 80            AND      #0x80            ; ... pro/novice settings
033A: C9 00            CMP      #0               ; Amateur?
033C: F0 05            BEQ      ExpP1Ama         ; Yes ... near the bottom of the screen
033E: A2 08            LDX      #8               ; Pro ... near the top
0340: 4C 45 03         JMP      ExpDone          ; Store and out
                  ExpP1Ama:
0343: A2 0C            LDX      #12              ; Novice ... near the bottom
                  ExpDone:
0345: 86 84            STX      PLAYR1Y          ; Player 1 Y coordinate
                  ExpNoP1:
0347: 60               RTS                       ; Done
                  
                  ADJUST_DIF:
                  
                  ;  This function adjusts the wall game difficulty values based on the
                  ;  current score. The music can also change with the difficulty. A single
                  ;  table describes the new values and when they take effect.
                  
0348: A2 00            LDX      #0               ; Starting at index 0
                  
                  AdjNextRow:
034A: BD F9 04         LDA      SKILL_VALUES,X   ; Get the score match
034D: C9 FF            CMP      #255             ; At the end of the table?
034F: D0 01            BNE      AdjCheckTable    ; No ... check this row
0351: 60               RTS                       ; End of the table ... leave it alone
                  AdjCheckTable:
0352: C5 8A            CMP      WALLCNT          ; Is this our row?
0354: D0 27            BNE      AdjBump          ; No ... bump to next
0356: E8               INX                       ; Copy ...
0357: BD F9 04         LDA      SKILL_VALUES,X   ; ... new ...
035A: 85 89            STA      WALL_INC         ; ... wall increment
035C: E8               INX                       ; Copy ...
035D: BD F9 04         LDA      SKILL_VALUES,X   ; ... new ...
0360: 85 8B            STA      WALLDELY         ; ... wall ...
0362: 85 8C            STA      WALLDELYR        ; ... delay
0364: E8               INX                       ; Copy ...
0365: BD F9 04         LDA      SKILL_VALUES,X   ; ... new ...
0368: 85 96            STA      GAPBITS          ; ... gap pattern
036A: E8               INX                       ; Copy ...
036B: BD F9 04         LDA      SKILL_VALUES,X   ; ... new ...
036E: 85 A4            STA      MUSAIND          ; ... MusicA index
0370: E8               INX                       ; Copy ...
0371: BD F9 04         LDA      SKILL_VALUES,X   ; ... new ...
0374: 85 A7            STA      MUSBIND          ; ... MusicB index
0376: A9 01            LDA      #1               ; Force ...
0378: 85 A3            STA      MUSADEL          ; ... music to ...
037A: 85 A6            STA      MUSBDEL          ; ... start new
037C: 60               RTS                       ; Done
                  AdjBump:
037D: E8               INX                       ; Move ...
037E: E8               INX                       ; ... X ...
037F: E8               INX                       ; ... to ...
0380: E8               INX                       ; ... next ...
0381: E8               INX                       ; ... row of ...
0382: E8               INX                       ; ... table
0383: 4C 4A 03         JMP      AdjNextRow       ; Try next row
                  
                  
                  SEL_RESET_CHK:
                  
                  ;  This function checks for changes to the reset/select
                  ;  switches and debounces the transitions.
                  ;  xxxxxxSR (Select, Reset)
                  
0386: A6 90            LDX      DEBOUNCE         ; Get the last value
0388: AD 82 02         LDA      SWCHB            ; New value
038B: 29 03            AND      #3               ; Only need bottom 2 bits
038D: C5 90            CMP      DEBOUNCE         ; Same as before?
038F: F0 07            BEQ      SelDebounce      ; Yes ... return nothing changed
0391: 85 90            STA      DEBOUNCE         ; Hold new last value
0393: 49 FF            EOR      #255             ; Active low to active high
0395: 29 03            AND      #3               ; Only need select/reset
0397: 60               RTS                       ; Return changes
                  SelDebounce:
0398: A9 00            LDA      #0               ; Return 0 ...
039A: 60               RTS                       ; ... nothing changed
                  
                  
                  INIT_MUSIC:
                  
                  ;  This function initializes the hardware and temporaries
                  ;  for 2-channel music
                  
039B: A9 06            LDA      #6               ; Audio control ...
039D: 85 15            STA      AUDC0            ; ... to pure ...
039F: 85 16            STA      AUDC1            ; ... tones
03A1: A9 00            LDA      #0               ; Turn off ...
03A3: 85 19            STA      AUDV0            ; ... all ...
03A5: 85 1A            STA      AUDV1            ; ... sound
03A7: 85 A4            STA      MUSAIND          ; Music pointers ...
03A9: 85 A7            STA      MUSBIND          ; ... to top of data
03AB: A9 01            LDA      #1               ; Force ...
03AD: 85 A3            STA      MUSADEL          ; ... music ...
03AF: 85 A6            STA      MUSBDEL          ; ... reload
03B1: A9 0F            LDA      #15              ; Set volume levels ...
03B3: 85 A5            STA      MUSAVOL          ; ... to ...
03B5: 85 A8            STA      MUSBVOL          ; ... maximum
03B7: 60               RTS                       ; Done
                  
                  PROCESS_MUSIC:
                  
                  ;  This function is called once per frame to process the
                  ;  2 channel music. Two tables contain the commands/notes
                  ;  for individual channels. This function changes the
                  ;  notes at the right time.
                  
03B8: C6 A3            DEC      MUSADEL          ; Current note on Channel A ended?
03BA: D0 58            BNE      MusDoB           ; No ... let it play
                  
                  MusChanA:
03BC: A6 A4            LDX      MUSAIND          ; Voice-A index
03BE: BD A0 04         LDA      MUSICA,X         ; Get the next music command
03C1: C9 00            CMP      #0               ; Jump?
03C3: F0 22            BEQ      MusCmdJumpA      ; Yes ... handle it
03C5: C9 01            CMP      #1               ; Control?
03C7: F0 11            BEQ      MusCmdCtrlA      ; Yes ... handle it
03C9: C9 02            CMP      #2               ; Volume?
03CB: D0 28            BNE      MusCmdToneA      ; No ... must be a note
03CD: E8               INX                       ; Point to volume value
03CE: E6 A4            INC      MUSAIND          ; Bump the music pointer
03D0: BD A0 04         LDA      MUSICA,X         ; Get the volume value
03D3: E6 A4            INC      MUSAIND          ; Bump the music pointer
03D5: 85 A5            STA      MUSAVOL          ; Store the new volume value
03D7: 4C BC 03         JMP      MusChanA         ; Keep processing through a tone
                  
                  MusCmdCtrlA:
03DA: E8               INX                       ; Point to the control value
03DB: E6 A4            INC      MUSAIND          ; Bump the music pointer
03DD: BD A0 04         LDA      MUSICA,X         ; Get the control value
03E0: E6 A4            INC      MUSAIND          ; Bump the music pointer
03E2: 85 15            STA      AUDC0            ; Store the new control value
03E4: 4C BC 03         JMP      MusChanA         ; Keep processing through a tone
                  
                  MusCmdJumpA:
03E7: E8               INX                       ; Point to jump value
03E8: 8A               TXA                       ; X to ...
03E9: A8               TAY                       ; ... Y (pointer to jump value)
03EA: E8               INX                       ; Point one past jump value
03EB: 8A               TXA                       ; Into A so we can subtract
03EC: 38               SEC                       ; New ...
03ED: F9 A0 04         SBC      MUSICA,Y         ; ... index
03F0: 85 A4            STA      MUSAIND          ; Store it
03F2: 4C BC 03         JMP      MusChanA         ; Keep processing through a tone
                  
                  MusCmdToneA:
03F5: A4 A5            LDY      MUSAVOL          ; Get the volume
03F7: 29 1F            AND      #0x1F            ; Lower 5 bits are frequency
03F9: C9 1F            CMP      #0x1F            ; Is this a silence?
03FB: D0 02            BNE      MusNoteA         ; No ... play it
03FD: A0 00            LDY      #0               ; Frequency of 31 flags silence
                  MusNoteA:
03FF: 85 17            STA      AUDF0            ; Store the frequency
0401: 84 19            STY      AUDV0            ; Store the volume
0403: BD A0 04         LDA      MUSICA,X         ; Get the note value again
0406: E6 A4            INC      MUSAIND          ; Bump to the next command
0408: 6A               ROR      A                ; The upper ...
0409: 6A               ROR      A                ; ... three ...
040A: 6A               ROR      A                ; ... bits ...
040B: 6A               ROR      A                ; ... hold ...
040C: 6A               ROR      A                ; ... the ...
040D: 29 07            AND      #7               ; ... delay
040F: 18               CLC                       ; No accidental carry
0410: 2A               ROL      A                ; Every delay tick ...
0411: 2A               ROL      A                ; ... is *4 frames
0412: 85 A3            STA      MUSADEL          ; Store the note delay
                  
                  MusDoB:
                  
0414: C6 A6            DEC      MUSBDEL
0416: D0 58            BNE      MusDoDone
                  
                  MusChanB:
0418: A6 A7            LDX      MUSBIND
041A: BD CD 04         LDA      MUSICB,X
041D: C9 00            CMP      #0
041F: F0 22            BEQ      MusCmdJumpB
0421: C9 01            CMP      #1
0423: F0 11            BEQ      MusCmdCtrlB
0425: C9 02            CMP      #2
0427: D0 28            BNE      MusCmdToneB
0429: E8               INX
042A: E6 A7            INC      MUSBIND
042C: BD CD 04         LDA      MUSICB,X
042F: E6 A7            INC      MUSBIND
0431: 85 A8            STA      MUSBVOL
0433: 4C 18 04         JMP      MusChanB
                  
                  MusCmdCtrlB:
0436: E8               INX
0437: E6 A7            INC      MUSBIND
0439: BD CD 04         LDA      MUSICB,X
043C: E6 A7            INC      MUSBIND
043E: 85 16            STA      AUDC1
0440: 4C 18 04         JMP      MusChanB
                  
                  MusCmdJumpB:
0443: E8               INX
0444: 8A               TXA
0445: A8               TAY
0446: E8               INX
0447: 8A               TXA
0448: 38               SEC
0449: F9 CD 04         SBC      MUSICB,Y
044C: 85 A7            STA      MUSBIND
044E: 4C 18 04         JMP      MusChanB
                  
                  MusCmdToneB:
0451: A4 A8            LDY      MUSBVOL
0453: 29 1F            AND      #0x1F
0455: C9 1F            CMP      #0x1F
0457: D0 02            BNE      MusNoteB
0459: A0 00            LDY      #0
                  MusNoteB:
045B: 85 18            STA      AUDF1
045D: 84 1A            STY      AUDV1
045F: BD CD 04         LDA      MUSICB,X
0462: E6 A7            INC      MUSBIND
0464: 6A               ROR      A
0465: 6A               ROR      A
0466: 6A               ROR      A
0467: 6A               ROR      A
0468: 6A               ROR      A
0469: 29 07            AND      #7
046B: 18               CLC
046C: 2A               ROL      A
046D: 2A               ROL      A
046E: 85 A6            STA      MUSBDEL
                  
                  MusDoDone:
0470: 60               RTS                       ; Done
                  
                  
                  INIT_GO_FX:
                  
                  ;  This function initializes the hardware and temporaries
                  ;  to play the soundeffect of a player hitting the wall
                  
0471: A9 05            LDA      #5               ; Set counter for frame delay ...
0473: 85 86            STA      MUS_TMP1         ; ... between frequency change
0475: A9 03            LDA      #3               ; Tone type ...
0477: 85 15            STA      AUDC0            ; ... poly tone
0479: A9 0F            LDA      #15              ; Volume A ...
047B: 85 19            STA      AUDV0            ; ... to max
047D: A9 00            LDA      #0               ; Volume B ...
047F: 85 1A            STA      AUDV1            ; ... silence
0481: A9 F0            LDA      #240             ; Initial ...
0483: 85 85            STA      MUS_TMP0         ; ... sound ...
0485: 85 17            STA      AUDF0            ; ... frequency
0487: 60               RTS                       ; Done
                  
                  PROCESS_GO_FX:
                  
                  ;  This function is called once per scanline to play the
                  ;  soundeffects of a player hitting the wall.
                  
0488: C6 86            DEC      MUS_TMP1         ; Time to change the frequency?
048A: D0 11            BNE      FxRun            ; No ... let it run
048C: A9 05            LDA      #5               ; Reload ...
048E: 85 86            STA      MUS_TMP1         ; ... the frame count
0490: E6 85            INC      MUS_TMP0         ; Increment ...
0492: A5 85            LDA      MUS_TMP0         ; ... the frequency divisor
0494: 85 17            STA      AUDF0            ; Change the frequency
0496: C9 00            CMP      #0
0498: D0 03            BNE      FxRun
049A: A9 01            LDA      #1               ; All done ... return 1
049C: 60               RTS                       ; Done
                  FxRun:
049D: A9 00            LDA      #0               ; Keep playing
049F: 60               RTS                       ; Done
                  
                  ;  Music commands for Channel A and Channel B
                  
                  ;  A word on music and wall timing ...
                  
                  ;  Wall moves between scanlines 0 and 111 (112 total)
                  
                  ;  Wall-increment   frames-to-top
                  ;       3             336
                  ;       2             224
                  ;       1             112
                  ;      0.5             56  ; Ah ... but we are getting one less
                  
                  ;  Each tick is multiplied by 4 to yield 4 frames per tick
                  ;  32 ticks/song = 32*4 = 128 frames / song
                  
                  ;  We want songs to start with wall at top ...
                  
                  ;  Find the least-common-multiple
                  ;  336 and 128 : 2688 8 walls, 21 musics
                  ;  224 and 128 :  896 4 walls,  7 musics
                  ;  112 and 128 :  896 8 walls,  7 musics
                  ;   56 and 128 :  896 16 walls, 7 musics
                  
                  ;  Wall moving every other gives us 112*2=224 scanlines
                  ;  Song and wall are at start every 4
                  ;  1 scanline, every 8
                  ;  Wall delay=3 gives us 128*3=336 scanlines 2
                  
                  .MUSCMD_JUMP      =     0      ; Music command value for JUMP
                  .MUSCMD_CONTROL   =     1      ; Music command value for CONTROL
                  .MUSCMD_VOLUME    =     2      ; Music command value for VOLUME
                  .MUS_REST         =     31     ; Frequency value for silence
                  .MUS_DEL_1        =     32*1   ; Note duration 1
                  .MUS_DEL_2        =     32*2   ; Note duration 2
                  .MUS_DEL_3        =     32*3   ; Note duration 3
                  .MUS_DEL_4        =     32*4   ; Note duration 4
                  
                  MUSICA:
                  
                  MA_SONG_1:
                  
04A0: 01 0C            .byte  MUSCMD_CONTROL, 12
04A2: 02 0F            .byte  MUSCMD_VOLUME,  15 ; Volume (full)
                  
                  MA1_01:
04A4: 6F               .byte  MUS_DEL_3  +  15
04A5: 3F               .byte  MUS_DEL_1  +  MUS_REST
04A6: 6F               .byte  MUS_DEL_3  +  15
04A7: 3F               .byte  MUS_DEL_1  +  MUS_REST
04A8: 27               .byte  MUS_DEL_1  +  7
04A9: 3F               .byte  MUS_DEL_1  +  MUS_REST
04AA: 27               .byte  MUS_DEL_1  +  7
04AB: 3F               .byte  MUS_DEL_1  +  MUS_REST
04AC: 5F               .byte  MUS_DEL_2  +  MUS_REST
04AD: 28               .byte  MUS_DEL_1  +  8
04AE: 3F               .byte  MUS_DEL_1  +  MUS_REST
04AF: 9F               .byte  MUS_DEL_4  +  MUS_REST
04B0: 51               .byte  MUS_DEL_2  +  17
04B1: 5F               .byte  MUS_DEL_2  +  MUS_REST
04B2: 51               .byte  MUS_DEL_2  +  17
04B3: 5F               .byte  MUS_DEL_2  +  MUS_REST
04B4: 70               .byte  MUS_DEL_3  +  16
04B5: 3F               .byte  MUS_DEL_1  +  MUS_REST
04B6: 00 14            .byte  MUSCMD_JUMP, (MA1_END - MA1_01) ; Repeat back to top
                  MA1_END:
                  
                  MA_SONG_2:
04B8: 01 0C            .byte  MUSCMD_CONTROL, 12
04BA: 02 0F            .byte  MUSCMD_VOLUME,  15
                  
                  MA2_01:
04BC: 2F               .byte  MUS_DEL_1  +  15
04BD: 3F               .byte  MUS_DEL_1  +  MUS_REST
04BE: 2F               .byte  MUS_DEL_1  +  15
04BF: 3F               .byte  MUS_DEL_1  +  MUS_REST
04C0: 5F               .byte  MUS_DEL_2  +  MUS_REST
04C1: 87               .byte  MUS_DEL_4  +  7
04C2: 9F               .byte  MUS_DEL_4  +  MUS_REST
04C3: 4F               .byte  MUS_DEL_2  +  15
04C4: 9F               .byte  MUS_DEL_4  +  MUS_REST
04C5: 4C               .byte  MUS_DEL_2  +  12
04C6: 5F               .byte  MUS_DEL_2  +  MUS_REST
04C7: 4F               .byte  MUS_DEL_2  +  15
04C8: 5F               .byte  MUS_DEL_2  +  MUS_REST
04C9: 51               .byte  MUS_DEL_2  +  17
04CA: 5F               .byte  MUS_DEL_2  +  MUS_REST
04CB: 00 11            .byte  MUSCMD_JUMP, (MA2_END - MA2_01) ; Repeat back to top
                  MA2_END:
                  
                  MUSICB:
                  
                  MB_SONG_1:
                  
04CD: 01 08            .byte  MUSCMD_CONTROL, 8
04CF: 02 08            .byte  MUSCMD_VOLUME,  8 ; Volume (half)
                  
                  MB1_01:
04D1: 2A               .byte  MUS_DEL_1  +  10
04D2: 3F               .byte  MUS_DEL_1  +  MUS_REST
04D3: 34               .byte  MUS_DEL_1  +  20
04D4: 3F               .byte  MUS_DEL_1  +  MUS_REST
04D5: 3E               .byte  MUS_DEL_1  +  30
04D6: 3F               .byte  MUS_DEL_1  +  MUS_REST
04D7: 2F               .byte  MUS_DEL_1  +  15
04D8: 3F               .byte  MUS_DEL_1  +  MUS_REST
04D9: 2A               .byte  MUS_DEL_1  +  10
04DA: 3F               .byte  MUS_DEL_1  +  MUS_REST
04DB: 34               .byte  MUS_DEL_1  +  20
04DC: 3F               .byte  MUS_DEL_1  +  MUS_REST
04DD: 3E               .byte  MUS_DEL_1  +  30
04DE: 3F               .byte  MUS_DEL_1  +  MUS_REST
04DF: 2F               .byte  MUS_DEL_1  +  15
04E0: 3F               .byte  MUS_DEL_1  +  MUS_REST
04E1: 00 12            .byte  MUSCMD_JUMP, (MB1_END - MB1_01) ; Repeat back to top
                  MB1_END:
                  
                  MB_SONG_2:
                  
04E3: 01 08            .byte  MUSCMD_CONTROL, 8
04E5: 02 08            .byte  MUSCMD_VOLUME,  8
                  
                  MB2_01:
04E7: 21               .byte  MUS_DEL_1  +  1
04E8: 3F               .byte  MUS_DEL_1  +  MUS_REST
04E9: 21               .byte  MUS_DEL_1  +  1
04EA: 3F               .byte  MUS_DEL_1  +  MUS_REST
04EB: 21               .byte  MUS_DEL_1  +  1
04EC: 3F               .byte  MUS_DEL_1  +  MUS_REST
04ED: 21               .byte  MUS_DEL_1  +  1
04EE: 3F               .byte  MUS_DEL_1  +  MUS_REST
04EF: 3E               .byte  MUS_DEL_1  +  30
04F0: 3F               .byte  MUS_DEL_1  +  MUS_REST
04F1: 3E               .byte  MUS_DEL_1  +  30
04F2: 3F               .byte  MUS_DEL_1  +  MUS_REST
04F3: 3E               .byte  MUS_DEL_1  +  30
04F4: 3F               .byte  MUS_DEL_1  +  MUS_REST
04F5: 3E               .byte  MUS_DEL_1  +  30
04F6: 3F               .byte  MUS_DEL_1  +  MUS_REST
04F7: 00 12            .byte  MUSCMD_JUMP, (MB2_END - MB2_01) ; Repeat back to top
                  MB2_END:
                  
                  SKILL_VALUES:
                  
                  ;  This table describes how to change the various
                  ;  difficulty parameters as the game progresses.
                  ;  For instance, the second entry in the table
                  ;  says that when the score is 4, change the values of
                  ;  wall-increment to 1, frame-delay to 2, gap-pattern to 0,
                  ;  MusicA to 24, and MusicB to 22.
                  
                  ;  A 255 on the end of the table indicates the end
                  
                  ;       Wall  Inc  Delay   Gap       MA                 MB
04F9: 00 01 03 00 00 00 .byte  0,     1,   3,     0  ,MA_SONG_1-MUSICA , MB_SONG_1-MUSICB
04FF: 04 01 02 00 18 16 .byte  4,     1,   2,     0  ,MA_SONG_2-MUSICA , MB_SONG_2-MUSICB
0505: 08 01 01 00 00 00 .byte  8,     1,   1,     0  ,MA_SONG_1-MUSICA , MB_SONG_1-MUSICB
050B: 10 01 01 01 18 16 .byte  16,    1,   1,     1  ,MA_SONG_2-MUSICA , MB_SONG_2-MUSICB
0511: 18 01 01 03 00 00 .byte  24,    1,   1,     3  ,MA_SONG_1-MUSICA , MB_SONG_1-MUSICB
0517: 20 01 01 07 18 16 .byte  32,    1,   1,     7  ,MA_SONG_2-MUSICA , MB_SONG_2-MUSICB
051D: 28 01 01 0F 00 00 .byte  40,    1,   1,    15  ,MA_SONG_1-MUSICA , MB_SONG_1-MUSICB
0523: 30 02 01 00 18 16 .byte  48,    2,   1,     0  ,MA_SONG_2-MUSICA , MB_SONG_2-MUSICB
0529: 40 02 01 01 00 00 .byte  64,    2,   1,     1  ,MA_SONG_1-MUSICA , MB_SONG_1-MUSICB
052F: 50 02 01 03 18 16 .byte  80,    2,   1,     3  ,MA_SONG_2-MUSICA , MB_SONG_2-MUSICB
0535: 60 02 01 07 00 00 .byte  96 ,   2,   1,     7  ,MA_SONG_1-MUSICA , MB_SONG_1-MUSICB
053B: FF               .byte  255
                  
                  GR_PLAYER:
                  ;  Image for players (8x8)
                  ;
053C: 10               .byte  0b__...1....
053D: 10               .byte  0b__...1....
053E: 28               .byte  0b__..1.1...
053F: 28               .byte  0b__..1.1...
0540: 54               .byte  0b__.1.1.1..
0541: 54               .byte  0b__.1.1.1..
0542: AA               .byte  0b__1.1.1.1.
0543: 7C               .byte  0b__.11111..
                  
                  DIGITS:
                  ;  Images for numbers
                  ;  We only need 5 rows, but the extra space on the end makes each digit 8 rows,
                  ;  which makes it the multiplication easier.
                  
0544: 0E               .byte 0b__....111.  ; 0 (leading 0 is blank)
0545: 0A               .byte 0b__....1.1.
0546: 0A               .byte 0b__....1.1.
0547: 0A               .byte 0b__....1.1.
0548: 0E               .byte 0b__....111.
0549: 00               .byte 0b__........
054A: 00               .byte 0b__........
054B: 00               .byte 0b__........
                  
054C: 22               .byte 0b__..1...1.  ; 1
054D: 22               .byte 0b__..1...1.
054E: 22               .byte 0b__..1...1.
054F: 22               .byte 0b__..1...1.
0550: 22               .byte 0b__..1...1.
0551: 00               .byte 0b__........
0552: 00               .byte 0b__........
0553: 00               .byte 0b__........
                  
0554: EE               .byte 0b__111.111.  ; 2
0555: 22               .byte 0b__..1...1.
0556: EE               .byte 0b__111.111.
0557: 88               .byte 0b__1...1...
0558: EE               .byte 0b__111.111.
0559: 00               .byte 0b__........
055A: 00               .byte 0b__........
055B: 00               .byte 0b__........
                  
055C: EE               .byte 0b__111.111.  ; 3
055D: 22               .byte 0b__..1...1.
055E: 66               .byte 0b__.11..11.
055F: 22               .byte 0b__..1...1.
0560: EE               .byte 0b__111.111.
0561: 00               .byte 0b__........
0562: 00               .byte 0b__........
0563: 00               .byte 0b__........
                  
0564: AA               .byte 0b__1.1.1.1.  ; 4
0565: AA               .byte 0b__1.1.1.1.
0566: EE               .byte 0b__111.111.
0567: 22               .byte 0b__..1...1.
0568: 22               .byte 0b__..1...1.
0569: 00               .byte 0b__........
056A: 00               .byte 0b__........
056B: 00               .byte 0b__........
                  
056C: EE               .byte 0b__111.111. ; 5
056D: 88               .byte 0b__1...1...
056E: EE               .byte 0b__111.111.
056F: 22               .byte 0b__..1...1.
0570: EE               .byte 0b__111.111.
0571: 00               .byte 0b__........
0572: 00               .byte 0b__........
0573: 00               .byte 0b__........
                  
0574: EE               .byte 0b__111.111. ; 6
0575: 88               .byte 0b__1...1...
0576: EE               .byte 0b__111.111.
0577: AA               .byte 0b__1.1.1.1.
0578: EE               .byte 0b__111.111.
0579: 00               .byte 0b__........
057A: 00               .byte 0b__........
057B: 00               .byte 0b__........
                  
057C: EE               .byte 0b__111.111. ; 7
057D: 22               .byte 0b__..1...1.
057E: 22               .byte 0b__..1...1.
057F: 22               .byte 0b__..1...1.
0580: 22               .byte 0b__..1...1.
0581: 00               .byte 0b__........
0582: 00               .byte 0b__........
0583: 00               .byte 0b__........
                  
0584: EE               .byte 0b__111.111. ; 8
0585: AA               .byte 0b__1.1.1.1.
0586: EE               .byte 0b__111.111.
0587: AA               .byte 0b__1.1.1.1.
0588: EE               .byte 0b__111.111.
0589: 00               .byte 0b__........
058A: 00               .byte 0b__........
058B: 00               .byte 0b__........
                  
058C: EE               .byte 0b__111.111. ; 9
058D: AA               .byte 0b__1.1.1.1.
058E: EE               .byte 0b__111.111.
058F: 22               .byte 0b__..1...1.
0590: EE               .byte 0b__111.111.
0591: 00               .byte 0b__........
0592: 00               .byte 0b__........
0593: 00               .byte 0b__........
                  
                  F7FA:
                  ; 6502 vectors
0594: 00 00            .word main
0596: 00 00            .word main  ; Reset vector (top of program)
0598: 00 00            .word main
                  
